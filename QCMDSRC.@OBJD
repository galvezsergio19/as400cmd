     A* @OBJ - Work with Objects   */
     A* Author: Sergio Galvez      */
     A                                      DSPSIZ(24 80 *DS3                  -
     A                                             27 132 *DS4)
     A                                      PRINT
     A                                      CA03
     A                                      CA05
     A                                      CA12
     A                                      CA13
     A                                      CA14
     A                                      MOUBTN(*ULP CA13)
     A                                      MOUBTN(*URP CA14)
     A                                      ENTFLDATR((*COLOR YLW) (*DSPATR UL))
     A                                      INDARA
     A          R SBFDTA1                   SFL
     A            HIDRISFL       1A  H
     A            HIDNDSFL       1A  H
     A            HIDSORTCRT     7A  H
     A            HIDSORTLST     7A  H
     A            HIDOTYPE      10A  H
     A            HIDOLIB       10A  H
     A            AOPTIONSFL     1A  P
     A            ARISFL         1A  P
     A            ANDSFL         1A  P
     A            SBFOPTION      1Y 0B 13  2EDTCDE( 4 ) DSPATR( &AOPTIONSFL )
     A            OBJNAME       10A  O 13  4DSPATR( &ARISFL )
     A            OBJTYPE        7A  O 13 15DSPATR( &ARISFL )
     A            OBJATTR        4A  O 13 23DSPATR( &ARISFL )
     A            OBJTEXT       19A  O 13 28DSPATR( &ARISFL )
     A            OBJSIZE        9Y 0O 13 48EDTCDE( 4 ) DSPATR( &ARISFL )
     A            CREATEDATE     6Y 0O 13 58EDTCDE( Y ) DSPATR( &ARISFL )
     A            LASTUSED       6Y 0O 13 67EDTCDE( Y ) DSPATR( &ANDSFL )
     A            DAYSUSED       4Y 0O 13 76EDTCDE( 3 ) DSPATR( &ARISFL )
     A*----------------------------------------------------------------
     A          R SBFCTL1                   SFLCTL( SBFDTA1 )
     A  *DS3                                SFLSIZ( 0180 )
     A  *DS4                                SFLSIZ( 0180 )
     A  *DS3                                SFLPAG( 0008 )
     A  *DS4                                SFLPAG( 0008 )
     A                                      OVERLAY
     A                                      RTNCSRLOC( &CURRCD &CURFLD )
     A  31                                  SFLDSP
     A  32                                  SFLDSPCTL
     A N32                                  SFLCLR
     A N34                                  SFLEND( *MORE )
     A            SFLRCDNBR      4S 0H      SFLRCDNBR( CURSOR )
     A            CURRCD        10A  H
     A            CURFLD        10A  H
     A            AOPTIONS       1A  P
     A            ASHOWUSR1      1A  P
     A            ASHOWUSR2      1A  P
     A            ANOTUSED       1A  P
     A                                  1  2'@OBJD'
     A                                      COLOR( BLU )
     A                                  1 23'Expanded Work With Objects'
     A                                      DSPATR( HI )
     A            SCDOW          9A  O  1 62COLOR( BLU )
     A                                  1 72DATE
     A                                      EDTWRD( '0  /  /  ' )
     A                                      COLOR( BLU )
     A                                  3  2'Object . . . . . . . . . . :'
     A            HEADEROBJ     10A  B  3 33DSPATR( HI )
     A                                  4  2'Library  . . . . . . . . . :'
     A            HEADERLIB     10A  B  4 33DSPATR( HI )
     A                                  5  2'Created by UserProfile . . :'
     A                                      DSPATR( &ASHOWUSR1 )
     A            P_CRTUSR      10A  O  5 33DSPATR( &ASHOWUSR2 )
     A                                  2 72SYSNAME
     A                                      COLOR( BLU )
     A                                  7  2'Type options, press Enter.'
     A                                      DSPATR( &AOPTIONS )
     A                                  8  4'1=Work Object'
     A                                      DSPATR( &AOPTIONS )
     A                                  8 24'2=Descn'
     A                                      DSPATR( &AOPTIONS )
     A                                  8 36'3=Object Lock'
     A                                      DSPATR( &AOPTIONS )
     A                                  8 52'4=Delete'
     A                                      DSPATR( &AOPTIONS )
     A                                  9  4'5=Clear PF Member'
     A                                      DSPATR( &AOPTIONS )
     A            DBUTILITY      8A  O  9 24DSPATR( &AOPTIONS )
     A                                  9 36'8=*All User'
     A                                      DSPATR( &AOPTIONS )
     A                                  9 52'9=Work Member'
     A                                      DSPATR( &AOPTIONS )
     A                                 11 12'Reverse image objects have no Last-
     A                                       Used date.'
     A                                      DSPATR( &ANOTUSED )
     A                                 11 68'Last'
     A                                      DSPATR( HI )
     A                                 11 74'Times'
     A                                      DSPATR( HI )
     A                                 12  2'Opt'
     A                                      DSPATR( HI )
     A                                 12  7'Object'
     A                                      DSPATR( HI )
     A                                 12 15'Type'
     A                                      DSPATR( HI )
     A                                 12 23'Attr'
     A                                      DSPATR( HI )
     A                                 12 28'Text'
     A                                      DSPATR( HI )
     A                                 12 50'Size(K)'
     A                                      DSPATR( HI )
     A                                 12 58'Created'
     A                                      DSPATR( HI )
     A                                 12 68'Used'
     A                                      DSPATR( HI )
     A                                 12 75'Used'
     A                                      DSPATR( HI )
      *----------------------------------------------------------------
     A          R SFOOTER1                  OVERLAY BLINK
     A                                 23  2'F3=Exit' COLOR( BLU )
     A                                 23 11'F5=Refresh' COLOR( BLU )
     A                                 23 24'F13=Sort Ascending' COLOR( BLU )
     A                                 23 45'F14=Sort Descending' COLOR( BLU )
     A                                 23 69'F12=Cancel' COLOR( BLU )
      *----------------------------------------------------------------
     A          R MSGSFL                    SFL SFLMSGRCD( 24 )
     A            MSGSFLKEY                 SFLMSGKEY
     A            PROGID                    SFLPGMQ( 10 )
     A          R MSGCTL                    SFLCTL( MSGSFL )
     A                                      SFLDSP SFLDSPCTL SFLINZ
     A N14                                  SFLEND
     A                                      SFLPAG( 01 ) SFLSIZ( 02 )
     A            PROGID                    SFLPGMQ( 10 )
